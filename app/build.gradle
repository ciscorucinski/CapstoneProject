apply plugin: 'com.android.application'
apply plugin: 'com.squareup.sqldelight'

android {
    signingConfigs {
        release_config {
            keyAlias 'release'
            keyPassword 'release'
            storeFile file('release_keystore.jks')
            storePassword 'release'
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    // Define custom labels for names
    ext {
        APP_NAME = "Introduction Resume"
        HUB_NAME = "Resume Hub"
    }
    defaultConfig {
        applicationId "io.github.ciscorucinski.personal.intro"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [applicationLabel: APP_NAME]
    }
    productFlavors {

        // Flavors for individual people / companies

        one_million {
            versionNameSuffix ' - 1MILLION Dance Studio'
            applicationIdSuffix 'one_million'
        }
        udacity {
            versionNameSuffix ' - Udacity'
            applicationIdSuffix 'udacity'
        }

        // Special testing flavor

        _testing {
            applicationIdSuffix 'testing'
        }

        // Special flavor just for the developer

        hub {
            versionNameSuffix ' - Hub'
            applicationIdSuffix 'hub'
            manifestPlaceholders = [applicationLabel: HUB_NAME]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release" || variant.flavorName == "hub") return
        variant.mergedFlavor.manifestPlaceholders = [applicationLabel: APP_NAME + variant.mergedFlavor.versionNameSuffix]
    }
    sourceSets {
        release { java.srcDirs = ['src/release/java', 'src/release/java/'] }
        hub { java.srcDirs = ['src/hub/java', 'src/hub/java/'] }
    }
    aaptOptions {
        noCompress 'pdf'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:customtabs:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.google.auto.value:auto-value:1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'
    testCompile 'junit:junit:4.12'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
}